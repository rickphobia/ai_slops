plugins {
    alias(libs.plugins.android.application)
    alias(libs.plugins.kotlin.compose)
    alias(libs.plugins.ktlint)
    alias(libs.plugins.detekt)
}

// The catalog holds the platform as "<major>.<minor>" because that is the SDK package's name.
val (compileSdkMajor, compileSdkMinor) =
    libs.versions.compileSdk
        .get()
        .split(".")
        .map(String::toInt)

// CI passes its run number so every build installs over the last one; a hand-made build is number 1.
val buildNumber =
    providers
        .environmentVariable("RICKNOTES_BUILD_NUMBER")
        .map(String::toInt)
        .getOrElse(1)

// The release key never enters the repo: it is read from these variables (see .env.example).
// Only packaging the release and preview APKs needs them, so lint, tests and debug builds work
// without them.
val releaseSigning =
    listOf(
        "RICKNOTES_KEYSTORE_FILE",
        "RICKNOTES_KEYSTORE_PASSWORD",
        "RICKNOTES_KEY_ALIAS",
        "RICKNOTES_KEY_PASSWORD",
    ).associateWith { name -> providers.environmentVariable(name).orNull?.takeIf(String::isNotBlank) }
val missingSigningVariables = releaseSigning.filterValues { it == null }.keys.toList()

android {
    namespace = "com.rickphobia.ricknotes"
    compileSdk {
        version = release(compileSdkMajor) { minorApiLevel = compileSdkMinor }
    }
    buildToolsVersion = libs.versions.buildTools.get()

    defaultConfig {
        applicationId = "com.rickphobia.ricknotes"
        minSdk =
            libs.versions.minSdk
                .get()
                .toInt()
        targetSdk =
            libs.versions.targetSdk
                .get()
                .toInt()
        versionCode = buildNumber
        // The same number as the release tag (ricknotes-v<N>), so Obtainium can match what is installed.
        versionName = buildNumber.toString()
    }

    signingConfigs {
        if (missingSigningVariables.isEmpty()) {
            create("release") {
                storeFile = file(releaseSigning.getValue("RICKNOTES_KEYSTORE_FILE")!!)
                storePassword = releaseSigning.getValue("RICKNOTES_KEYSTORE_PASSWORD")
                keyAlias = releaseSigning.getValue("RICKNOTES_KEY_ALIAS")
                keyPassword = releaseSigning.getValue("RICKNOTES_KEY_PASSWORD")
            }
        }
    }

    buildTypes {
        release {
            signingConfig = signingConfigs.findByName("release")
        }
        // Pull-request builds: a separate app with its own settings, so an unmerged build never
        // replaces RickNotes or touches its Study folder. Its label is in src/preview/res.
        create("preview") {
            initWith(getByName("release"))
            applicationIdSuffix = ".preview"
            versionNameSuffix = "-preview"
        }
    }

    buildFeatures {
        compose = true
        buildConfig = true
    }

    lint {
        warningsAsErrors = true
        abortOnError = true
        // Version bumps are a deliberate change in libs.versions.toml, not something a new
        // release upstream should turn into a red build overnight.
        disable += setOf("GradleDependency", "AndroidGradlePluginVersion", "NewerVersionAvailable", "OldTargetApi")
    }
}

kotlin {
    jvmToolchain(
        libs.versions.jdk
            .get()
            .toInt(),
    )
}

dependencies {
    implementation(project(":core"))

    implementation(libs.androidx.core.ktx)
    implementation(libs.androidx.lifecycle.runtime.ktx)
    implementation(libs.androidx.lifecycle.runtime.compose)
    implementation(libs.androidx.activity.compose)
    implementation(platform(libs.androidx.compose.bom))
    implementation(libs.androidx.compose.ui)
    implementation(libs.androidx.compose.ui.tooling.preview)
    implementation(libs.androidx.compose.material3)
    implementation(libs.androidx.ink.authoring)
    implementation(libs.androidx.ink.brush)
    implementation(libs.androidx.ink.strokes)
    implementation(libs.androidx.ink.rendering)

    testImplementation(libs.junit)
}

// Without this, a release build with no key would quietly produce an unsigned APK.
val checkReleaseSigning by tasks.registering {
    description = "Fails, naming the missing variables, when the release signing key is not configured."
    val missing = missingSigningVariables
    doLast {
        if (missing.isNotEmpty()) {
            throw GradleException(
                "Release signing is not configured: set ${missing.joinToString()} " +
                    "(see .env.example and the README's \"Signing key\" section).",
            )
        }
    }
}

tasks.matching { it.name == "packageRelease" || it.name == "packagePreview" }.configureEach {
    dependsOn(checkReleaseSigning)
}

ktlint {
    version.set(libs.versions.ktlint)
    android.set(true)
}

detekt {
    buildUponDefaultConfig = true
    config.setFrom(rootProject.file("config/detekt.yml"))
}
